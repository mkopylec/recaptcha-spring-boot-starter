plugins {
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
    id 'nebula.optional-base' version '3.0.3'
    id 'com.github.kt3k.coveralls' version '2.3.1'
    id 'maven'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.3.3'
}

scmVersion {
    tag {
        prefix = ''
    }
}

group 'com.github.mkopylec'
archivesBaseName = name
version scmVersion.version

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.1-1'
    compile group: 'org.springframework', name: 'spring-web', version: '4.2.4.RELEASE'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: '1.3.2.RELEASE'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '1.3.2.RELEASE', optional
    compile group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: '1.3.2.RELEASE', optional

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.3.2.RELEASE'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.3.2.RELEASE'
    testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.0-groovy-2.4'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '1.58'
}

configurations {
    all*.exclude group: 'org.mortbay.jetty', module: 'servlet-api'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    if (project.ext.has('signArtifacts')) {
        sign configurations.archives
    }
}

ext {
    ossrhUsername = project.ext.has('ossrhUsername') ? project.ext.ossrhUsername : ''
    ossrhPassword = project.ext.has('ossrhPassword') ? project.ext.ossrhPassword : ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name 'reCAPTCHA Spring Boot Starter'
                packaging 'jar'
                description 'Spring Boot starter for Google\'s reCAPTCHA'
                url 'https://github.com/mkopylec/recaptcha-spring-boot-starter'
                scm {
                    connection 'scm:git:https://github.com/mkopylec/recaptcha-spring-boot-starter.git'
                    developerConnection 'scm:git:https://github.com/mkopylec/recaptcha-spring-boot-starter.git'
                    url 'https://github.com/mkopylec/recaptcha-spring-boot-starter'
                }
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mkopylec'
                        name 'Mariusz Kopylec'
                        email 'mariusz.kopylec@o2.pl'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

